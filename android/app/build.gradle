apply plugin: 'com.android.library'
apply from: '../prepare-robolectric.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.all { t ->
            reports {
                html.enabled true
            }
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "      ${result.resultType} (${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)     "
                    def repeatLength = output.length()
                    println '\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength) + '\n'

                    println "see report at file://${t.reports.html.destination}/index.html"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'com.android.support:design:28.0.0'
    implementation "com.android.support:appcompat-v7:28.0.0"

    // node_modules
    implementation "com.facebook.react:react-native:+"

    // third party
    implementation 'com.github.MikeChugunov:ahbottomnavigation:2.1.0.2'
    implementation 'com.balysv.materialmenu:material-menu-toolbar:1.5.4'

    // tests
    testImplementation "junit:junit:4.12"
    testImplementation "org.robolectric:robolectric:3.1.4"
    testImplementation 'org.assertj:assertj-core:2.5.0'
}
